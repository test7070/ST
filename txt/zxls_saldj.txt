zxls_saldj:--打卡上傳
declare @t_gowork nvarchar(10)='08:00' --正常上班時間+公司規定
declare @t_offwork nvarchar(10)='17:00' --正常下班時間-公司規定
declare @t_gowork2 nvarchar(10)='13:00' --中午上班時間
declare @t_offwork2 nvarchar(10)='12:00' --中午下班時間
--加班根據加班單
--禮拜六休假
--1A上班 2A下班
--106/03/31 增加
--5A外出 6A返回

declare @t_weekday nvarchar(10)--星期

IF OBJECT_ID('tempdb..#tmp')is not null
BEGIN
   drop table #tmp
END

IF OBJECT_ID('tempdb..#bbm')is not null
BEGIN
   drop table #bbm
END

IF OBJECT_ID('tempdb..#bbs')is not null
BEGIN
   drop table #bbs
END

--避免上次不正常關閉 先將 cursor 關閉
BEGIN TRY
	close cursor_table
	deallocate cursor_table
END TRY
BEGIN CATCH
END CATCH
BEGIN TRY
	close cursor_table2
	deallocate cursor_table2
END TRY
BEGIN CATCH
END CATCH

--拆解傳入的資料
create table #tmp(
	datea nvarchar(100),
	timea nvarchar(100),
	sssno nvarchar(100),
	namea nvarchar(100),
	wtype nvarchar(100),
	noa nvarchar(100)
)

insert into #tmp
select SUBSTRING(a.a,3,8)datea,SUBSTRING(a.a,11,4) timea,b.noa,b.namea
,SUBSTRING(a.a,15,2) work,a.noa--,SUBSTRING(a.a,17,10) barcode
from ztmpxls a left join sss b on SUBSTRING(a.a,17,10)=b.barcode
where a.a!='' and b.noa is not null

--delete #tmp where not(wtype='1A' or wtype='2A')

update #tmp
set datea=dbo.AD2ChineseEraName(datea),timea=left(timea,2)+':'+RIGHT(timea,2)

--bbm
create table #bbm(
		noa nvarchar(20),
		w133 float,
		w166 float,
		w100 float,
		mount float,
		holiday bit,
)

--bbs
create table #bbs(
		noa nvarchar(20),
		noq nvarchar(10),
		sssno nvarchar(20),
		namea nvarchar(50),
		clockin nvarchar(50),
		clockout nvarchar(50),
		w133 float,
		w166 float,
		w100 float,
		memo nvarchar(50),
		typea nvarchar(50)
)

declare @datea nvarchar(MAX)
declare @sssno nvarchar(MAX)
declare @namea nvarchar(MAX)
declare @timea nvarchar(MAX)
declare @wtype nvarchar(MAX)
declare @tsssno nvarchar(MAX)=''
declare @pwtype nvarchar(MAX)=''
declare @count int=0
declare @t_count int=0

--select * from #tmp order by sssno,datea,timea,noa

declare cursor_table cursor for
select datea from #tmp group by datea order by datea --匯入日期
open cursor_table
fetch next from cursor_table
into @datea
while(@@FETCH_STATUS <> -1)
begin
	set @pwtype='XX'
	set @tsssno=''
	set @count=1
	
	--處理正常上下班--------------------------------------------------------------------
	declare cursor_table2 cursor for
	select sssno,namea,timea,wtype from #tmp where datea=@datea and (wtype='1A' or wtype='2A') order by sssno,datea,timea,noa
	open cursor_table2
	fetch next from cursor_table2
	into @sssno,@namea,@timea,@wtype
	while(@@FETCH_STATUS <> -1)
	begin
		if(@sssno!=@tsssno)
		begin
			set @pwtype='XX'
		end
	
		if(@wtype='1A')
		begin
			insert #bbs	
			select  @datea,@count,@sssno,namea,@timea,'',0,0,0,'','1'
			from sss a where isnull(outdate,'')='' and noa=@sssno order by noa
			set @t_count=@count
			set @count=@count+1
		end
		if(@wtype='2A')
		begin
			if(@pwtype='1A')
			begin
				update #bbs	set clockout=@timea	where noq=@t_count
			end
			else
			begin
				insert #bbs	
				select  @datea,@count,@sssno,namea,'',@timea,0,0,0,'','1'
				from sss a where isnull(outdate,'')='' and noa=@sssno order by noa
				set @t_count=0
				set @count=@count+1	
			end
		end
		
		set @pwtype=@wtype
		set @tsssno=@sssno
		
		fetch next from cursor_table2
		into @sssno,@namea,@timea,@wtype
	end
	close cursor_table2
	deallocate cursor_table2
	
	--106/03/31--處理外出返回--------------------------------------------------------------------
	declare cursor_table2 cursor for
	select sssno,namea,timea,wtype from #tmp where datea=@datea and (wtype='5A' or wtype='6A') order by sssno,datea,timea,noa
	open cursor_table2
	fetch next from cursor_table2
	into @sssno,@namea,@timea,@wtype
	while(@@FETCH_STATUS <> -1)
	begin
		if(@sssno!=@tsssno)
		begin
			set @pwtype='XX'
		end
	
		if(@wtype='5A')
		begin
			insert #bbs	
			select  @datea,@count,@sssno,namea,@timea,'',0,0,0,'','2'
			from sss a where isnull(outdate,'')='' and noa=@sssno order by noa
			set @t_count=@count
			set @count=@count+1
		end
		if(@wtype='6A')
		begin
			if(@pwtype='5A')
			begin
				update #bbs	set clockout=@timea	where noq=@t_count
			end
			else
			begin
				insert #bbs	
				select  @datea,@count,@sssno,namea,'',@timea,0,0,0,'','1'
				from sss a where isnull(outdate,'')='' and noa=@sssno order by noa
				set @t_count=0
				set @count=@count+1	
			end
		end
		
		set @pwtype=@wtype
		set @tsssno=@sssno
		
		fetch next from cursor_table2
		into @sssno,@namea,@timea,@wtype
	end
	close cursor_table2
	deallocate cursor_table2
	
	--------------------------------------------------------------------------------------
	
	insert #bbs	
	select  @datea,ROW_NUMBER() over (order by a.noa)+@count,noa,namea,'','',0,0,0,'','1'
	from sss a where (isnull(outdate,'')='' or isnull(outdate,'')>@datea)
	and not exists(select * from #bbs where sssno=a.noa) order by noa
	
	update a
	set noq=rr
	from (select noq,ROW_NUMBER() over (order by sssno,clockin,cast(noq as int))rr  from #bbs)a
		
	update a
	set noq=right(REPLICATE('0',case when xlen>3 then xlen else 3 end)+noq,case when xlen>3 then xlen else 3 end)
	from #bbs a outer apply (select len(MAX(cast(noq as int)))xlen from #bbs)b
	
	set @t_weekday=DATEPART(WEEKDAY, dbo.ChineseEraName2AD(@datea))-1
	
	--非假日
	if((@datea not in (select noa from holiday where isnull(iswork,0)!=1) --不是假日
		and @t_weekday!='0' and @t_weekday!='6')--不是禮拜六日
		or @datea in (select noa from holiday where isnull(iswork,0)=1)--上班日
	)
	begin
		--105/07/07 出勤加班時數 直接計算出 比對加班單核准 加班時數
		update #bbs
		set w133=case when clockout>=@t_offwork and isnull(clockin,'')!='' and isnull(clockout,'')!='' 
		then round((cast(datediff(MINUTE, cast(dbo.ChineseEraName2AD(noa) as nvarchar(20))+' '+@t_offwork
		, cast(dbo.ChineseEraName2AD(noa) as nvarchar(20))+' '+clockout) as float)/60),1) else 0 end
		where isnull(clockin,'')!='' and isnull(clockout,'')!='' and clockout>=@t_offwork and typea='1'
		
		--105/07/07 加班時數小時0.5 不列入加班時數
		update #bbs set w133=case when w133<0.5 then 0 else w133 end where typea='1'
		update #bbs set w166=case when w133>2 then w133-2 else 0 end where typea='1'
		update #bbs set w133=case when w133>2 then 2 else w133 end where typea='1'
		
		--更新備註
		update a
		set memo=case when clockin>@t_gowork and isnull((select count(*) from salvacause where (@datea between bdate and edate) and sssno=a.sssno and a.clockin between btime and (case when etime=@t_offwork2 then @t_gowork2 else etime end ) ),0)=0 then '遲到' else '' end
		from #bbs a
		where isnull(a.clockin,'')!='' and typea='1'
		
		update a
		set memo=memo+
		case when clockout<@t_offwork and isnull((select count(*) from salvacause where (@datea between bdate and edate) and sssno=a.sssno and a.clockout between (case when btime=@t_gowork2 then @t_offwork2 else btime end) and etime ),0)=0 then (case when len(memo)>0 then ',' else '' end) +'早退' else '' end
		from #bbs a
		where isnull(a.clockout,'')!='' and typea='1'
		
		update a
		set memo=case when counts!=0 then '請假' else '' end
		from #bbs a 
		outer apply (select count(*) counts from salvacause where (@datea between bdate and edate) and sssno=a.sssno and btime=@t_gowork and etime=@t_offwork )b
		where isnull(a.clockin,'')='' and isnull(a.clockout,'')=''  and typea='1'
		
		update a set memo='外出' from #bbs a where typea='2'
	end
	else
	begin
		update #bbs
		set w100=case when isnull(clockin,'')!='' and isnull(clockout,'')!='' then
		round((cast(datediff(MINUTE, cast(dbo.ChineseEraName2AD(noa) as nvarchar(20))+' '+case when clockin>@t_gowork then clockin else @t_gowork end 
		, cast(dbo.ChineseEraName2AD(noa) as nvarchar(20))+' '+case when clockout<@t_offwork then clockout else @t_offwork end) as float)/60),1) else 0 end
		where isnull(clockin,'')!='' and isnull(clockout,'')!='' and typea='1'
	end
	
	insert #bbm
	select @datea,SUM(w133),SUM(w166),SUM(w100),(select COUNT(*) from (select sssno from #bbs group by sssno)tmp)
	,case when 
	(@datea not in (select noa from holiday where isnull(iswork,0)!=1) --不是假日
	and @t_weekday!='0' and @t_weekday!='6')--不是禮拜六日
	or @datea in (select noa from holiday where isnull(iswork,0)=1) --上班日
	then 0 else 1 end
	from #bbs
	
	if((select COUNT(*) from salpresent where noa=@datea)=0)
	begin
		--新增bbm
		insert salpresent(noa,w133,w166,w100,mount,holiday)
		select * from #bbm
		
		--新增bbs
		delete salpresents where noa=@datea
		insert salpresents(noa,noq,sssno,namea,clockin,clockout,w133,w166,w100,memo)
		select noa,noq,sssno,namea,clockin,clockout,w133,w166,w100,memo from #bbs 
	end
	else
	begin
		delete a from salpresents a 
		where exists (select * from #bbs where noa=a.noa and sssno=a.sssno)
		
		insert salpresents(noa,noq,sssno,namea,clockin,clockout,w133,w166,w100,memo)
		select noa,'x'+noq,sssno,namea,clockin,clockout,w133,w166,w100,memo from #bbs a
		where not exists (select * from salpresents where noa=a.noa and sssno=a.sssno)
	
		update a
		set w100=isnull((select sum(w100) from salpresents where noa=a.noa),0)
		,w133=isnull((select sum(w133) from salpresents where noa=a.noa),0)
		,w166=isnull((select sum(w166) from salpresents where noa=a.noa),0)
		,mount=isnull((select count(*) from (select sssno from salpresents where noa=a.noa group by sssno)tmp),0)
		from salpresent a
		where a.noa=@datea
		
		--更新排序順序
		update salpresents
		set noq='XX'+noq
		where noa=@datea
		
		update a
		set noq=rr
		from (select noq,ROW_NUMBER() over (order by sssno,noq )rr  from salpresents where noa=@datea)a
		
		update salpresents
		set noq=right('000'+noq,3)
		where noa=@datea
	end
	
	delete #bbm
	delete #bbs
	
	fetch next from cursor_table
	into @datea
end
close cursor_table
deallocate cursor_table


IF OBJECT_ID('tempdb..#tmp')is not null
BEGIN
   drop table #tmp
END

IF OBJECT_ID('tempdb..#bbm')is not null
BEGIN
   drop table #bbm
END

IF OBJECT_ID('tempdb..#bbs')is not null
BEGIN
   drop table #bbs
END
;